import java.text.SimpleDateFormat
def mainClassName = project.property('mainClass')
def releaseVersion = !version.endsWith("SNAPSHOT")

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes(
                "Name"                  : project.name,
                "Manifest-Version"      : project.version,
                "Main-Class"            : mainClassName,
                "Implementation-Title"  : "$groupPackage" + "." + "$rootProject.name",
                "Implementation-Version": project.version,
                "Implementation-Vendor" : "$projectLeaderName",
                "Created-By"            : "Gradle ${gradle.gradleVersion}",
                "Built-By"              : "$projectLeaderName",
                "Build-Timestamp"       : new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                "Build-Jdk"             : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                "Build-OS"              : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}")
    }
}

jar.doLast {
    def createIzPackInstallerStringProperty = project.property('createIzPackInstaller')
    def createIzPackInstaller = createIzPackInstallerStringProperty.toBoolean()
    if (releaseVersion || createIzPackInstaller) {
        def pkAlias = project.property("$mysticCryptKeyAlias")
        def keystoreFileName = project.property("$mysticCryptStoreFile")
        def storePassword = project.property("$mysticCryptStorePassword")
        def keyPassword = project.property("$mysticCryptKeyPassword")
        def keystoreType = project.property("$mysticCryptStoreType")
        def libsFilePath = "$buildDir/libs"
        def libFiles = files { file(libsFilePath).listFiles() }
        def destDir = "$buildDir/signed"
        def signedDir = new File(destDir)
        signedDir.mkdirs()
        libFiles.each {
            ant.signjar(
                    jar: it,
                    destDir: "$destDir",
                    alias: "$pkAlias",
                    storetype: "$keystoreType",
                    keystore: "$keystoreFileName",
                    storepass: "$storePassword",
                    keypass: "$keyPassword",
                    preservelastmodified: "true"
            )
        }
    } else {
        println('Created jar file is not signed.\n' +
                'IzPackInstaller can not be created because it depends on signed jar.')
    }
}

task withAllDependendiesJar(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest.from jar.manifest
    archiveClassifier = "all"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task createIzPackInstallerFromSignedJar(dependsOn: [clean, build, withAllDependendiesJar, jar, izPackCreateInstaller]) {
    withAllDependendiesJar.mustRunAfter(clean)
    build.mustRunAfter(withAllDependendiesJar)
    jar.mustRunAfter(build)
    izPackCreateInstaller.mustRunAfter(jar)
}

